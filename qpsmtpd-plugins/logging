sub init {
	my ($self, $qp) = @_;
	$self->isa_plugin("vmail/common");
	$self->vm_debug;
	$self->SUPER::init($qp);
}

sub register {
	my ($self, $qp, %args) = @_;

	my $loglevel = $args{loglevel};

	$self->{_level} = LOGWARN;
	if (defined($loglevel)) {
		if ($loglevel =~ /^\d+$/) {
			$self->{_level} = $loglevel;
		} else {
			$self->{_level} = log_level($loglevel);
		}
	}

	$self->SUPER::init($qp, $loglevel);
}

sub vm_init_config {
	my ($self, $config_fields, $config_fields_empty, $config_fields_default)  = @_;
	%$config_fields         = ();
	%$config_fields_empty   = ();
	%$config_fields_default = ();
}

sub hook_logging {
	my ($self, $transaction, $trace, $hook, $plugin, @log) = @_;

	# Don't log your own log enries! 
	return DECLINED if defined $plugin and $plugin eq $self->plugin_name;

	# Return if we haven't initialised yet
	return DECLINED if not $self->{_level};

	# Return if the level is above our log level
	return DECLINED if ($trace > $self->{_level});

	# If we haven't connected yet we can't do anything.
	return DECLINED if not $self->vm_connected;

	# Get the transaction_id
	my $transaction_id = $self->vm_notes('transaction_id') || undef;

	# Log the message
	$transaction_id = $self->vm_call("qpsmtpd.log", $transaction_id, $hook,
		$plugin, $trace, join(" ", @log));
	
	# Store the transaction_id for future use
	$self->vm_notes('transaction_id', $transaction_id);
	
	return DECLINED;
}

sub hook_queue_pre {
	my ($self, $transaction) = @_;

	my $transaction_id = $self->vm_notes('transaction_id');
	my $sender         = lc $transaction->sender->address;
	my $user           = lc $self->qp->connection->notes("authuser");
	my $client_ip      = lc $self->qp->connection->remote_ip;
	my $subject        = $transaction->header->get('Subject');
	my $size           = $transaction->data_size;

	# Remove the trailing newline and sanitize.
	$subject =~ s/\n$//g;
	$subject =~ m/(.*)/;
	$subject = $1;

	# Build the recipients option
	my @recipients = ();
	foreach my $rcpt ($transaction->recipients) {
		my $addr = lc $rcpt->address;
		push(@recipients, $addr);
	}

	$self->vm_call("qpsmtpd.log_transaction", $transaction_id, $sender,
		$user, $subject, $size, $client_ip, \@recipients);

	return DECLINED;
}

# vim: ft=perl
